<?xml version="1.0" encoding="UTF-8"?>

<!--
**********************************************************
 NRTask.xml
 New Relic Agent background task.
 Minimum requirements: FW 7.2

 Copyright 2019 New Relic Inc. All Rights Reserved. 
**********************************************************
-->

<component name="com.newrelic.NewRelicAgent.NRTask" extends="Task">

	<interface>
		<!-- Properties -->
		<field id="accountNumber" type="string" value=""/>
		<field id="apiKey" type="string" value=""/>
    </interface>

	<script type = "text/brightscript" >
		<![CDATA[
		sub init()
  			m.top.functionName = "nrTaskMain"
		end sub
		
		function nrInsertInsightsData(attributes as Object) as Object
		    url = box("https://insights-collector.newrelic.com/v1/accounts/" + m.top.accountNumber + "/events")
		    apikey = m.top.apiKey
		    jsonString = FormatJson(attributes)
		
		    urlReq = CreateObject("roUrlTransfer")
		
		    urlReq.SetUrl(url)
		    urlReq.RetainBodyOnError(true)
		    urlReq.EnablePeerVerification(false)
		    urlReq.EnableHostVerification(false)
		    urlReq.AddHeader("Content-Type", "application/json")
		    urlReq.AddHeader("X-Insert-Key", apikey)
		
		    resp = urlReq.PostFromString(jsonString)
		    
		    return resp
		end function

		function nrEventProcessor() as Void
		    while true
		        ev = m.nr.callFunc("nrExtractEvent")
		        if ev = invalid then exit while
		        res = nrInsertInsightsData(ev)
		        if res <> 200
		            m.nr.callFunc("nrLog", "-- nrEventProcessor: FAILED, retry later --")
		            m.nr.callFunc("nrRecordEvent", ev)
		            return
		        else
		            m.nr.callFunc("nrLog", "-- nrEventProcessor: insert insights data --")
		        end if
		    end while
		end function
		
		function nrTaskMain() as Void
			'We assume that parent node is NewRelicAgent
			m.nr = m.top.getParent()
			m.nr.callFunc("nrLog", "---- NRTASK MAIN ----")
			nrEventProcessor()
		end function
		]]>
	</script>
</component>
